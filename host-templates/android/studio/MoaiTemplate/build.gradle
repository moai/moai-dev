// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'


        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

//common settings for all but our app
subprojects {
    apply plugin: MoaiModulePlugin
    
    //get our build artifacts out of src
    buildDir = new File(rootProject.projectDir, "gradleBuild/" + project.name)

    //android defaults
    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        println("configuring: " + project.name)
        apply plugin: 'com.android.library'
    }

    android {
        compileSdkVersion 22
        buildToolsVersion "22.0.1"

        defaultConfig {
            minSdkVersion 17
            targetSdkVersion 22
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

}



class MoaiModulePlugin implements Plugin<Project> {
    void apply(Project project) {
        project.extensions.create("moai", MoaiModuleExtension)
    }
}

class MoaiModuleExtension {
    String nativeLib
}