apply plugin: MoaiSettings

class MoaiSettings implements Plugin<Settings> {
    void apply(Settings settings) {
        //get any enabled modules

        def libRoot = "app/libmoai"
        if (settings.hasProperty('moaiLibRoot')) {
            libRoot = settings.moaiLibRoot
        } else {
            settings.moaiLibRoot = libRoot
        }
        if (!settings.hasProperty('moaiModules') || settings.moaiModules == null) {
            settings.moaiModules=""
        }
        def modules = settings.moaiModules.toString().split(",").each { i -> i.trim() }

        //include enabled modules projects
        def moddir = new File(libRoot+"/modules/external")

        if (moddir.exists()) {
            moddir.eachDir() { dir ->
                def modulename = dir.name
                dir.eachDirMatch(~/project/) { projectDir ->
                    if (modules.contains(modulename)) {
                        //some projects are nested, find our build.gradle
                        def dirs = projectDir.listFiles().findAll() { d -> d.isDirectory() }
                        dirs.add(0,projectDir)
                        for (subdir in dirs) {
                            if (subdir.listFiles().find { f -> f.name.equals('build.gradle') } != null) {
                                settings.include(":modules:moaiexternal_${modulename}")
                                settings.project(":modules:moaiexternal_${modulename}").projectDir = new File(subdir.path)
                                break
                            }
                        }
                    }
                }
            }
        }
    }
}